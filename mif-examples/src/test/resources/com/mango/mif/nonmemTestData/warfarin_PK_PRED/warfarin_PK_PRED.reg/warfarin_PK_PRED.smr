THETA:      POP_CL      POP_V       POP_KA      POP_TLAG    
ETA:        PPV_CL      PPV_V       PPV_KA      PPV_TLAG    
ERR:        RUV_PROP    RUV_ADD     
warfarin_PK_PRED.lst	250.627	FOCEI	eval=414 sig=3.0 sub=32 obs=250 VER(Betaa6.5) 
THETA     = 0.135       7.99        1.28        0.865

ETASD     = 0.267955    0.143875    0.764199    0.343511
ETAR12    = 0.136

ETAPval   = 0.99474     0.81662     0.81456     0.74856
ETAshr%   = 1.5763      9.8696      46.509      59.26
EPSshr%   = 14.199      14.888

EPSSD     = 0.001       0.669328

MINIMIZATION SUCCESSFUL
PARAMETER ESTIMATE IS NEAR ITS BOUNDARY
THIS MUST BE ADDRESSED BEFORE THE COVARIANCE STEP CAN BE IMPLEMENTED

 Ttot 0:2.29 Test 0:1.48 Tcov 0:0 Ttcl 0:0.81


$PROB WARFARIN PK
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551

;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177

$INPUT ID TIME WT AGE SEX AMT DVID DV MDV
$DATA ..\warfarin_conc_pca.csv IGNORE=#
IGNORE (DVID.EQ.2) ; IGNORE PCA OBSERVATIONS

$EST METHOD=COND INTER 
MAX=9990 SIG=3 NOABORT ;PRINT=1
$COV

$THETA
(0.001,0.1) ; POP_CL L/H/70KG
(0.001,8)   ; POP_V  L/70KG
(0.001,2) ; POP_KA H-1
(0.001,1) ; POP_TLAG H

$OMEGA BLOCK(2)
0.1 ; PPV_CL
0.01 0.1 ; PPV_V
$OMEGA
0.1 ; PPV_KA
0.1 ; PPV_TLAG

$SIGMA 
0.01 ; RUV_PROP
0.05 ; RUV_ADD MG/L


$PRED
   IF (AMT.GT.0) DOSE=AMT
   ; COVARIATE MODEL
   GRPCL=THETA(1)*(WT/70)**0.75
   GRPV=THETA(2)*WT/70
   GRPKA=THETA(3)
   GRPLG=THETA(4)

   ; INDIVIDUAL PARAMETERS
   CL=GRPCL*EXP(ETA(1))
   V=GRPV*EXP(ETA(2))
   KA=GRPKA*EXP(ETA(3))
   TLAG=GRPLG*EXP(ETA(4))

   IF (TIME.LE.TLAG) THEN
      CONC=0
   ELSE
      TNOW=TIME-TLAG
      K=CL/V
      CONC=DOSE*KA/(V*(KA-K))*(EXP(-K*TNOW) - EXP(-KA*TNOW))
   ENDIF
   Y=CONC*(1+ERR(1))+ERR(2)

$TABLE ID TIME WT SEX AGE ; COVARIATES
CL V KA TLAG ; EBE ESTIMATES
DVID Y ; PREDICTIONS
ONEHEADER NOPRINT FILE=warfarin_pk_pred.fit




